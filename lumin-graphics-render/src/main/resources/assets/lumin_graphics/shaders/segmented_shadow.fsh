#version 410 core

#ifdef LUMIN_VULKAN
#define LUMIN_BINDING(index) layout(set = 0, binding = index)
#define LUMIN_VERTEX_ID gl_VertexIndex
#else
#define LUMIN_BINDING(index)
#define LUMIN_VERTEX_ID gl_VertexID
#endif

layout(location = 0) in vec2 f_Position;

layout(std140) LUMIN_BINDING(0) uniform SegmentedShadowUniforms {
    vec4 ShadowColor;
    vec4 ShadowInfo;
    vec4 SegmentRects[64];
    vec4 SegmentRadii[64];
};

layout(location = 0) out vec4 fragColor;

const int DIRECTION_PAIR_COUNT = 8;
const int SAMPLE_COUNT = 5;
const vec2 SAMPLE_DIRECTIONS[DIRECTION_PAIR_COUNT] = vec2[DIRECTION_PAIR_COUNT](
    vec2(1.0, 0.0),
    vec2(0.92387953, 0.38268343),
    vec2(0.70710678, 0.70710678),
    vec2(0.38268343, 0.92387953),
    vec2(0.0, 1.0),
    vec2(-0.38268343, 0.92387953),
    vec2(-0.70710678, 0.70710678),
    vec2(-0.92387953, 0.38268343)
);

float roundedRectDistance(vec2 position, vec4 rect, float radius) {
    vec2 halfSize = rect.zw * 0.5;
    vec2 center = rect.xy + halfSize;
    halfSize += vec2(0.5);
    vec2 distance = abs(position - center) - halfSize + radius;
    return length(max(distance, 0.0)) + min(max(distance.x, distance.y), 0.0) - radius;
}

float segmentedDistance(vec2 position, int count) {
    float distance = 1e20;
    for (int i = 0; i < 64; i++) {
        if (i >= count) break;
        distance = min(distance, roundedRectDistance(position, SegmentRects[i], max(0.0, SegmentRadii[i].x)));
    }
    return distance;
}

float maskAt(vec2 position, int count, float antialias) {
    return 1.0 - smoothstep(0.0, antialias, segmentedDistance(position, count));
}

void main() {
    int count = int(ShadowInfo.y);
    float blurRadius = max(0.0, ShadowInfo.x);
    if (count <= 0 || blurRadius <= 0.0) discard;

    float baseDistance = segmentedDistance(f_Position, count);
    float edgeAntialias = max(fwidth(baseDistance), 0.0001);
    if (baseDistance <= -edgeAntialias || baseDistance >= blurRadius) discard;

    vec2 positionWidth = fwidth(f_Position);
    float antialias = max(max(positionWidth.x, positionWidth.y) * 2.0, 0.0001);
    float blurredMask = maskAt(f_Position, count, antialias);

    for (int directionIndex = 0; directionIndex < DIRECTION_PAIR_COUNT; directionIndex++) {
        vec2 direction = SAMPLE_DIRECTIONS[directionIndex];
        for (int sampleIndex = 1; sampleIndex <= SAMPLE_COUNT; sampleIndex++) {
            float sampleScale = float(sampleIndex) / float(SAMPLE_COUNT);
            vec2 offset = direction * blurRadius * sampleScale;
            blurredMask += maskAt(f_Position + offset, count, antialias);
            blurredMask += maskAt(f_Position - offset, count, antialias);
        }
    }

    blurredMask *= 1.0 / 81.0;
    float outsideCoverage = smoothstep(-edgeAntialias, edgeAntialias, baseDistance);
    float fadeWidth = min(antialias, blurRadius);
    float rangeCoverage = 1.0 - smoothstep(blurRadius - fadeWidth, blurRadius, max(baseDistance, 0.0));
    float alpha = ShadowColor.a * clamp(blurredMask * outsideCoverage * rangeCoverage, 0.0, 1.0);
    if (alpha < 0.001) discard;

    fragColor = vec4(ShadowColor.rgb, alpha);
}
